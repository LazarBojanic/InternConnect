import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeHotReload)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.room)
}

kotlin {
    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
        }
    }

    listOf(
        iosX64(),
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "ComposeApp"
            isStatic = true
            linkerOpts.add("-lsqlite3")
        }
    }


	sourceSets {
        commonMain.dependencies {
			implementation(libs.androidxCoreKtx)
			implementation(libs.androidxAppcompat)
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
	        implementation(compose.materialIconsExtended)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)

	        implementation(libs.kotlinxCoroutinesCore)
	        implementation(libs.kotlinxSerializationJson)

	        implementation(libs.androidxLifecycleViewModelSavedState)
	        implementation(libs.androidxLifecycleViewmodelCompose)
	        implementation(libs.androidxLifecycleRuntimeCompose)
	        implementation(libs.androidxNavigationCompose)
	        implementation(libs.androidxDatastore)
	        implementation(libs.androidxDatastorePreferences)

	        api(libs.koinCore)
	        api(libs.koinAnnotations)
	        implementation(libs.koinCompose)
	        implementation(libs.koinComposeViewModel)

	        implementation(libs.roomRuntime)
	        implementation(libs.sqliteBundled)

	        implementation(libs.ktorSerializationKotlinxJson)
            implementation(libs.ktorClientCore)
	        implementation(libs.ktorClientContentNegotiation)
	        implementation(libs.ktorClientLogging)
	        implementation(libs.squareupOkio)

	        implementation(libs.oAuthJavaJwt)

        }
        androidMain.dependencies {
            implementation(compose.preview)

            implementation(libs.androidxActivityCompose)

            implementation(libs.kotlinxCoroutinesAndroid)

	        implementation(libs.koinAndroid)
	        implementation(libs.koinAndroidXCompose)

	        implementation(libs.roomRuntimeAndroid)
	        implementation(libs.sqliteWrapper)

            implementation(libs.ktorClientOkHttp)

        }
        iosMain.dependencies {
            implementation(libs.ktorClientDarwin)
        }
        iosX64Main.dependencies {
	        implementation(libs.kotlinxCoroutinesCoreIosX64)
	        implementation(libs.koinCoreIosX64)
	        implementation(libs.roomRuntimeIosX64)
        }
        iosArm64Main.dependencies {
	        implementation(libs.kotlinxCoroutinesCoreIosArm64)
	        implementation(libs.koinCoreIosArm64)
	        implementation(libs.roomRuntimeIosArm64)
        }
        iosSimulatorArm64Main.dependencies {
	        implementation(libs.kotlinxCoroutinesCoreIosSimulatorArm64)
	        implementation(libs.koinCoreIosSimulatorArm64)
	        implementation(libs.roomRuntimeIosSimulatorArm64)
        }
    }
}


android {
    namespace = "com.internconnect.internconnectfrontendclient"
    compileSdk = libs.versions.androidCompileSdk.get().toInt()

    defaultConfig {
        applicationId = "com.internconnect.internconnectfrontendclient"
        minSdk = libs.versions.androidMinSdk.get().toInt()
        targetSdk = libs.versions.androidTargetSdk.get().toInt()
        versionCode = 1
        versionName = "1.0"
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

room {
    schemaDirectory("$projectDir/schemas")
}

dependencies {
    add("kspAndroid", libs.roomCompiler)
    add("kspIosArm64", libs.roomCompiler)
    add("kspIosX64", libs.roomCompiler)
    add("kspIosSimulatorArm64", libs.roomCompiler)

	add("kspAndroid", libs.koinKspCompiler)
	add("kspIosX64", libs.koinKspCompiler)
	add("kspIosArm64", libs.koinKspCompiler)
	add("kspIosSimulatorArm64", libs.koinKspCompiler)
    debugImplementation(compose.uiTooling)
}

ksp{
	arg("KOIN_USE_COMPOSE_VIEWMODEL", "true")
	arg("KOIN_CONFIG_CHECK", "true")
}
